#include <WiFi.h>
#include <HTTPClient.h>
#include <Preferences.h>
#include <time.h>
#include "state_types.h"

// Optional overrides from external file (generated by Windows tool)
#if __has_include("credentials.h")
#include "credentials.h"
#endif

// Wi-Fi (defaults if not overridden)
#ifndef WIFI_SSID_VALUE
#define WIFI_SSID_VALUE "YUSUF KÜPELİ"
#endif
#ifndef WIFI_PASS_VALUE
#define WIFI_PASS_VALUE "yusuf1234"
#endif
#ifndef API_URL_VALUE
#define API_URL_VALUE "https://interteks-randiman-server.onrender.com/ingest"
#endif
#ifndef TEZGAH_ID_VALUE
#define TEZGAH_ID_VALUE "T-001"
#endif

const char* WIFI_SSID = WIFI_SSID_VALUE;
const char* WIFI_PASS = WIFI_PASS_VALUE;
const char* API_URL = API_URL_VALUE;
const char* TEZGAH_ID = TEZGAH_ID_VALUE;

unsigned long currentEpochSeconds(unsigned long fallbackMs) {
  time_t now = time(nullptr);
  if (now > 1609459200) {
    return static_cast<unsigned long>(now);
  }
  return fallbackMs / 1000;
}

// GPIO pins (PC187 output -> ESP32 input with pull-up)
const int PIN_SARI = 32;
const int PIN_KIRMIZI = 33;
const int PIN_YESIL = 25;
const int PIN_BEYAZ = 26;
const int PIN_MAVI = 27;
                                                                                                         
// Röle 3.3V veriyor (aktif-yuksek)
// 0 = aktif-dusuk (opto/PC187), 1 = aktif-yuksek (dogrudan 3.3V)
#define INPUT_ACTIVE_HIGH 1

// Timings
const unsigned long SAMPLE_INTERVAL_MS = 200;
const unsigned long SEND_INTERVAL_MS = 10000;
const unsigned long WIFI_RETRY_MS = 10000;
const unsigned long WIFI_CONNECT_TIMEOUT_MS = 15000;
const unsigned long SAVE_INTERVAL_MS = 60000;

// Timezone (Turkey UTC+3)
const long GMT_OFFSET_SEC = 3 * 3600;
const int DAYLIGHT_OFFSET_SEC = 0;

const char* STATE_NAMES[] = { "sari", "kirmizi", "yesil", "beyaz", "mavi", "none" };

unsigned long lastSampleMs = 0;
unsigned long lastSendMs = 0;
unsigned long lastStateChangeMs = 0;
unsigned long lastWifiAttemptMs = 0;
unsigned long lastSaveMs = 0;
State currentState = STATE_NONE;

uint64_t stateDurationsMs[5] = {0, 0, 0, 0, 0};
Preferences prefs;

int currentMonthKey() {
  struct tm timeinfo;
  if (!getLocalTime(&timeinfo)) {
    return -1;
  }
  return (timeinfo.tm_year + 1900) * 12 + (timeinfo.tm_mon + 1);
}

void loadDurations() {
  stateDurationsMs[STATE_SARI] = prefs.getULong64("sari", 0);
  stateDurationsMs[STATE_KIRMIZI] = prefs.getULong64("kirmizi", 0);
  stateDurationsMs[STATE_YESIL] = prefs.getULong64("yesil", 0);
  stateDurationsMs[STATE_BEYAZ] = prefs.getULong64("beyaz", 0);
  stateDurationsMs[STATE_MAVI] = prefs.getULong64("mavi", 0);
}

void saveDurations() {
  prefs.putULong64("sari", stateDurationsMs[STATE_SARI]);
  prefs.putULong64("kirmizi", stateDurationsMs[STATE_KIRMIZI]);
  prefs.putULong64("yesil", stateDurationsMs[STATE_YESIL]);
  prefs.putULong64("beyaz", stateDurationsMs[STATE_BEYAZ]);
  prefs.putULong64("mavi", stateDurationsMs[STATE_MAVI]);
}

void resetDurations() {
  for (int i = 0; i < 5; i++) {
    stateDurationsMs[i] = 0;
  }
  saveDurations();
}

bool isLampOn(int pin) {
#if INPUT_ACTIVE_HIGH
  return digitalRead(pin) == HIGH;
#else
  // PC187 output is active-low when lamp is ON
  return digitalRead(pin) == LOW;
#endif
}

State readState() {
  // Priority order if multiple lamps are on (run light last)
  if (isLampOn(PIN_KIRMIZI)) return STATE_KIRMIZI;
  if (isLampOn(PIN_SARI)) return STATE_SARI;
  if (isLampOn(PIN_YESIL)) return STATE_YESIL;
  if (isLampOn(PIN_BEYAZ)) return STATE_BEYAZ;
  if (isLampOn(PIN_MAVI)) return STATE_MAVI;
  return STATE_NONE;
}

const char* readActiveLabel() {
  // Sarı + Yeşil birlikte yanarsa özel durum
  if (isLampOn(PIN_KIRMIZI)) return "kirmizi";
  bool sariOn = isLampOn(PIN_SARI);
  bool yesilOn = isLampOn(PIN_YESIL);
  if (sariOn && yesilOn) return "iplik_sonu";
  if (sariOn) return "sari";
  if (yesilOn) return "yesil";
  if (isLampOn(PIN_BEYAZ)) return "beyaz";
  if (isLampOn(PIN_MAVI)) return "mavi";
  return "none";
}

void addDurationToCurrent(unsigned long nowMs) {
  if (currentState == STATE_NONE) return;
  uint64_t elapsed = (uint64_t)(nowMs - lastStateChangeMs);
  if (currentState <= STATE_MAVI) {
    stateDurationsMs[currentState] += elapsed;
  }
  lastStateChangeMs = nowMs;
}

void connectWiFi() {
  WiFi.mode(WIFI_STA);
  WiFi.setSleep(false);
  WiFi.begin(WIFI_SSID, WIFI_PASS);
  unsigned long startMs = millis();
  while (WiFi.status() != WL_CONNECTED && (millis() - startMs) < WIFI_CONNECT_TIMEOUT_MS) {
    delay(200);
  }
}

void setup() {
  Serial.begin(115200);
  delay(200);
  Serial.println();
  Serial.println("ESP32 basladi.");

#if INPUT_ACTIVE_HIGH
  pinMode(PIN_SARI, INPUT_PULLDOWN);
  pinMode(PIN_KIRMIZI, INPUT_PULLDOWN);
  pinMode(PIN_YESIL, INPUT_PULLDOWN);
  pinMode(PIN_BEYAZ, INPUT_PULLDOWN);
  pinMode(PIN_MAVI, INPUT_PULLDOWN);
#else
  pinMode(PIN_SARI, INPUT_PULLUP);
  pinMode(PIN_KIRMIZI, INPUT_PULLUP);
  pinMode(PIN_YESIL, INPUT_PULLUP);
  pinMode(PIN_BEYAZ, INPUT_PULLUP);
  pinMode(PIN_MAVI, INPUT_PULLUP);
#endif

  prefs.begin("randiman", false);
  loadDurations();

  connectWiFi();
  configTime(GMT_OFFSET_SEC, DAYLIGHT_OFFSET_SEC, "pool.ntp.org", "time.nist.gov");

  unsigned long nowMs = millis();
  lastSampleMs = nowMs;
  lastSendMs = nowMs;
  lastStateChangeMs = nowMs;
  lastWifiAttemptMs = nowMs;
  lastSaveMs = nowMs;
  currentState = readState();
  Serial.print("Baslangic durumu: ");
  Serial.println(readActiveLabel());

  int storedMonth = prefs.getInt("month", -1);
  int nowMonth = currentMonthKey();
  if (nowMonth != -1 && storedMonth != nowMonth) {
    prefs.putInt("month", nowMonth);
    resetDurations();
  }
}

void loop() {
  unsigned long nowMs = millis();

  if (nowMs - lastSampleMs >= SAMPLE_INTERVAL_MS) {
    State newState = readState();
    if (newState != currentState) {
      Serial.print("Durum degisti: ");
      Serial.print(STATE_NAMES[currentState]);
      Serial.print(" -> ");
      Serial.println(STATE_NAMES[newState]);
      addDurationToCurrent(nowMs);
      currentState = newState;
    }
    lastSampleMs = nowMs;
  }

  if (nowMs - lastSendMs >= SEND_INTERVAL_MS) {
    addDurationToCurrent(nowMs);

    if (WiFi.status() != WL_CONNECTED) {
      if (nowMs - lastWifiAttemptMs >= WIFI_RETRY_MS) {
        lastWifiAttemptMs = nowMs;
        connectWiFi();
      }
    }

    if (WiFi.status() == WL_CONNECTED) {
      HTTPClient http;
      http.setConnectTimeout(5000);
      http.setTimeout(5000);
      http.begin(API_URL);
      http.addHeader("Content-Type", "application/json");

      char payload[512];
      // Durations are cumulative seconds since boot
      snprintf(
        payload, sizeof(payload),
        "{"
          "\"tezgahId\":\"%s\","
          "\"timestamp\":%lu,"
          "\"aktifDurum\":\"%s\","
          "\"states\":{"
            "\"sari\":%lu,"
            "\"kirmizi\":%lu,"
            "\"yesil\":%lu,"
            "\"beyaz\":%lu,"
            "\"mavi\":%lu"
          "}"
        "}",
        TEZGAH_ID,
        currentEpochSeconds(nowMs),
        readActiveLabel(),
        (unsigned long)(stateDurationsMs[STATE_SARI] / 1000),
        (unsigned long)(stateDurationsMs[STATE_KIRMIZI] / 1000),
        (unsigned long)(stateDurationsMs[STATE_YESIL] / 1000),
        (unsigned long)(stateDurationsMs[STATE_BEYAZ] / 1000),
        (unsigned long)(stateDurationsMs[STATE_MAVI] / 1000)
      );

      Serial.print("POST gonderiliyor: ");
      Serial.println(payload);
      int httpCode = http.POST(payload);
      Serial.print("HTTP kodu: ");
      Serial.println(httpCode);
      http.end();
    } else {
      Serial.println("WiFi bagli degil, POST atlanacak.");
    }

    lastSendMs = nowMs;
  }

  if (nowMs - lastSaveMs >= SAVE_INTERVAL_MS) {
    int nowMonth = currentMonthKey();
    if (nowMonth != -1) {
      int storedMonth = prefs.getInt("month", -1);
      if (storedMonth != nowMonth) {
        prefs.putInt("month", nowMonth);
        resetDurations();
      }
    }
    saveDurations();
    lastSaveMs = nowMs;
  }
}
